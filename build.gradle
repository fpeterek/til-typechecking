plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
    id 'antlr'
    id 'java'
}

group 'org.fpeterek.til'
version '1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.7.0'

    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.14.0'
    implementation 'org.json:json:20220320'
    implementation 'org.apache.commons:commons-text:1.9'

    antlr 'org.antlr:antlr4:4.9.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

generateGrammarSource {
    arguments += ['-visitor']
}

compileKotlin {
    dependsOn(generateGrammarSource)
}

sourceSets {
    main {
        kotlin {
            srcDir generateGrammarSource.outputDirectory
        }
    }
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': 'org.fpeterek.til.typechecking.MainKt'
    }
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude 'META-INF', 'META-INF/**'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
